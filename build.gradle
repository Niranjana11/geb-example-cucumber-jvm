buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.7"

		// if you use the version of cucumber-jvm that the plugin was built with
		// (1.1.6 for version 0.7), the line below is unnecessary
		classpath "info.cukes:cucumber-core:1.1.8"
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: "com.github.samueltbrown.cucumber"

repositories {
	jcenter()
}

ext {
	groovyVersion = '2.3.6'
	gebVersion = '0.9.3'
	seleniumVersion = '2.42.2'
	cucumberJvmVersion = '1.1.8'
}

dependencies {
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
	testCompile "org.gebish:geb-core:$gebVersion"
	testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"

	testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
	testCompile "info.cukes:cucumber-groovy:$cucumberJvmVersion"

	// pick whichever drivers you want. We test against most of them here.
	// see src/test/resources/GebConfig.groovy for how to switch between them
	cucumberRuntime "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
	cucumberRuntime "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
	cucumberRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:$seleniumVersion"
	cucumberRuntime("com.github.detro.ghostdriver:phantomjsdriver:1.1.0") {
		// phantomjs driver pulls in a different selenium version
		transitive = false
	}
}

cucumber {
	formats = [
		'pretty', // prints nice format out to the console
		'html:build/reports/cucumber', // html
		'junit:build/cucumber.xml' // junit format for integration with CI tool etc
	]
}
